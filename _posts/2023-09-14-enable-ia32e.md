---
layout: post
title: "enable IA-32e mode"
category: x86
tags: [x86]
---
{% include JB/setup %}

只有在保护模式下才能开启IA-32e mode，所以要先开启模式。

### 开启保护模式

根据intel SDM volume3 9.9.1的描述：

```
we recommend that you follow these steps:
1. Disable interrupts. A CLI instruction disables maskable hardware interrupts. NMI interrupts can be disabled with external circuitry. (Software must guarantee that no exceptions or interrupts are generated during the mode switching operation.)
2. Execute the LGDT instruction to load the GDTR register with the base address of the GDT.
3. Execute a MOV CR0 instruction that sets the PE flag (and optionally the PG flag) in control register CR0.
4. Immediately following the MOV CR0 instruction, execute a far JMP or far CALL instruction. (This operation is typically a far jump or call to the next instruction in the instruction stream.)
5. The JMP or CALL instruction immediately after the MOV CR0 instruction changes the flow of execution and
serializes the processor.
6. If paging is enabled, the code for the MOV CR0 instruction and the JMP or CALL instruction must come from a page that is identity mapped (that is, the linear address before the jump is the same as the physical address after paging and protected mode is enabled). The target instruction for the JMP or CALL instruction does not need to be identity mapped.
7. If a local descriptor table is going to be used, execute the LLDT instruction to load the segment selector for the LDT in the LDTR register.
8. Execute the LTR instruction to load the task register with a segment selector to the initial protected-mode task or to a writable area of memory that can be used to store TSS information on a task switch.
9. After entering protected mode, the segment registers continue to hold the contents they had in real-address mode. The JMP or CALL instruction in step 4 resets the CS register. Perform one of the following operations to update the contents of the remaining segment registers.
— Reload segment registers DS, SS, ES, FS, and GS. If the ES, FS, and/or GS registers are not going to be
used, load them with a null selector.
— Perform a JMP or CALL instruction to a new task, which automatically resets the values of the segment
registers and branches to a new code segment.
10. Execute the LIDT instruction to load the IDTR register with the address and limit of the protected-mode IDT.
11. Execute the STI instruction to enable maskable hardware interrupts and perform the necessary hardware
operation to enable NMI interrupts
```

- 需要开启A20地址线。
- 开启保护模式前需要CLI关闭中断(NMI中断需特殊方式关闭)。
- 准备GDT等相关数据，LGDT加载到GDTR寄存器中。
- 置位CR0.PE开启保护模式。
- 代码段寄存器使用jmp/call far刷新缓存，数据段寄存器需要重载(mov)刷新缓存。
- TSS、LDT、IDT、分页都是可选的，如果最终目的是为了开启IA-32e，那么可以不进行相关操作。



### 开启IA-32e模式

根据intel SDM volume3 9.8.5的描述：

```
Operating systems should follow this sequence to initialize IA-32e mode:
1. Starting from protected mode, disable paging by setting CR0.PG = 0. Use the MOV CR0 instruction to disable
paging (the instruction must be located in an identity-mapped page).
2. Enable physical-address extensions (PAE) by setting CR4.PAE = 1. Failure to enable PAE will result in a #GP fault when an attempt is made to initialize IA-32e mode.
3. Load CR3 with the physical base address of the Level 4 page map table (PML4).
4. Enable IA-32e mode by setting IA32_EFER.LME = 1.
5. Enable paging by setting CR0.PG = 1. This causes the processor to set the IA32_EFER.LMA bit to 1. The MOV
CR0 instruction that enables paging and the following instructions must be located in an identity-mapped page
(until such time that a branch to non-identity mapped pages can be effected).
```

- 复位CR0.PG、置位CR4.PAE、置位IA32_EFER.LME(开启IA-32e)、置位CR0.PG的操作顺序要严格遵守。
- TSS、descriptor、页表都相较于保护模式发现巨大变化，因此需要重新准备数据和重载相关寄存器。



### 代码

```
[SECTION gdt]  
LABEL_GDT:      dd  0,0 
LABEL_DESC_CODE32:  dd  0x0000FFFF,0x00CF9A00
LABEL_DESC_DATA32:  dd  0x0000FFFF,0x00CF9200
GdtLen  equ $ - LABEL_GDT
GdtPtr  dw  GdtLen - 1 
    dd  LABEL_GDT
SelectorCode32  equ LABEL_DESC_CODE32 - LABEL_GDT
SelectorData32  equ LABEL_DESC_DATA32 - LABEL_GDT

[SECTION gdt64]
LABEL_GDT64:        dq  0x0000000000000000
LABEL_DESC_CODE64:  dq  0x0020980000000000
LABEL_DESC_DATA64:  dq  0x0000920000000000
GdtLen64    equ $ - LABEL_GDT64
GdtPtr64    dw  GdtLen64 - 1
        dd  LABEL_GDT64
SelectorCode64  equ LABEL_DESC_CODE64 - LABEL_GDT64
SelectorData64  equ LABEL_DESC_DATA64 - LABEL_GDT64
  
;=======    open address A20
    push    ax
    in  al, 92h
    or  al, 00000010b
    out 92h,    al
    pop ax

;=======    init GDT goto protect mode   
    cli        
  	db  0x66
    lgdt    [GdtPtr]
 
    mov eax,    cr0
    or  eax,    1
    mov cr0,    eax
  
    jmp dword SelectorCode32:GO_TO_TMP_Protect
[SECTION .s32]
[BITS 32] 
  
GO_TO_TMP_Protect: 
;=======    go to tmp long mode
    mov ax, 0x10
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov ss, ax
    mov esp,    7E00h
  
;=======    init temporary page table 0x90000
    mov dword   [0x90000],  0x91007
    mov dword   [0x90800],  0x91007    
    mov dword   [0x91000],  0x92007
    mov dword   [0x92000],  0x000083  
    mov dword   [0x92008],  0x200083
    mov dword   [0x92010],  0x400083
    mov dword   [0x92018],  0x600083
	mov dword   [0x92020],  0x800083
	mov dword   [0x92028],  0xa00083

;=======    load GDTR
    db  0x66
    lgdt    [GdtPtr64]
    mov ax, 0x10
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov gs, ax
    mov ss, ax
  
    mov esp,    7E00h
  
;=======    open PAE
    mov eax,    cr4
    bts eax,    5
    mov cr4,    eax

;=======    load    cr3
    mov eax,    0x90000
    mov cr3,    eax
  
;=======    enable long-mode
    mov ecx,    0C0000080h      ;IA32_EFER
    rdmsr
  
    bts eax,    8
    wrmsr
  
;=======    open PE and paging
    mov eax,    cr0
    bts eax,    0
    bts eax,    31
    mov cr0,    eax
  
    jmp SelectorCode64:StartCode64
;TODO: lidt、ltr、sti
```

